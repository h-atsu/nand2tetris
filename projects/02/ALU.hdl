// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // 0変更処理
    Mux16(a=x , b=false , sel=zx , out= wzx);
    Mux16(a=y , b=false , sel=zy , out= wzy);

    // 反転処理
    Not16(in=wzx , out=negwzx);
    Not16(in=wzy , out=negwzy);

    Mux16(a=wzx , b=negwzx , sel=nx , out=wx);
    Mux16(a=wzy , b=negwzy , sel=ny , out=wy);

    //加算,論理積計算
    Add16(a=wx , b=wy , out=addout);
    And16(a=wx , b=wy , out=andout);
    Mux16(a=andout , b=addout , sel=f , out=wout);

    //出力反転処理
    Not16(in=wout , out=negwout);
    Mux16(a=wout , b=negwout , sel=no , out=out , out[0..7]=out0to7 , out[8..15]=out8to15 , out[15]=ng);

    // (out == 0)
    Or8Way(in=out0to7 , out=or0to7);
    Or8Way(in=out8to15 , out=or8to15);
    Or(a=or0to7 , b=or8to15 , out=wor);
    Not(in=wor , out=zr);





}